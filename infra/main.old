param spokeLocation string
param hubLocation string
param imageTag string
param acrName string
param dbAdminUserName string
param dbAdminPassword string
param dbSizeGB int = 32
param tags object = {
  environment: 'dev'
  costcode: '1234567890'
}

var frontEndContainerImage = '${acr.properties.loginServer}/frontend:${imageTag}'
var backendContainerImage = '${acr.properties.loginServer}/backend:${imageTag}'
var acrLoginServer = '${acrName}.azurecr.io'
var acrAdminPassword = listCredentials(acr.id, '2021-12-01-preview').passwords[0].value
var affix = uniqueString(resourceGroup().id)
var workspaceName = '${affix}-wks'
var containerAppEnvName = '${affix}-app-env'
var backendAppName = 'backend'
var frontendAppName = 'frontend'
var sbNamespace = 'checkins'
var cosmosName = '${affix}-cosmosdb'
var cosmosDbName = 'checkinDb'
var cosmosPartitionKey = 'user_id'
var aiName = '${affix}-ai'
var suffix = uniqueString(resourceGroup().id)
var secrets = [
  {
    name: 'registry-password'
    value: acrAdminPassword
  }
]

module hubVnetModule 'modules/vnet.bicep' = {
  name: 'module-hub-vnet'
  params: {
    location: hubLocation
    vnetName: 'hub-vnet-${suffix}'
    addressSpace: '10.0.0.0/16'
    subnets: [
      {
        name: 'DBSubnet'
        addressPrefix: '10.0.0.0/24'
        delegations: [
          {
            name: 'app-server-delegation'
            properties: {
              serviceName: 'Microsoft.DBforPostgreSQL/flexibleServers'
            }
          }
        ]
      }
    ]
  }
}

module spokeVnetModule 'modules/vnet.bicep' = {
  name: 'module-spoke-vnet'
  params: {
    location: spokeLocation
    vnetName: 'spoke-vnet-${suffix}'
    addressSpace: '10.1.0.0/16'
    subnets: [
      {
        name: 'ControlPlaneSubnet'
        addressPrefix: '10.1.8.0/21'
        delegations: null
      }
      {
        name: 'AppSubnet'
        addressPrefix: '10.1.16.0/21'
        delegations: null
      }
    ]
  }
}

module peeringModule 'modules/peering.bicep' = {
  name: 'module-peering'
  params: {
    vNets: concat(array(hubVnetModule.outputs.vnet), array(spokeVnetModule.outputs.vnet))
    suffix: suffix
    isGatewayDeployed: false
  }
}

module aiModule 'modules/ai.bicep' = {
  name: 'aiDeployment'
  params: {
    location: spokeLocation
    aiName: aiName
  }
}

resource acr 'Microsoft.ContainerRegistry/registries@2021-12-01-preview' existing = {
  name: acrName
}

module wksModule 'modules/wks.bicep' = {
  name: 'wksDeployment'
  params: {
    location: spokeLocation
    name: workspaceName
    tags: tags
  }
}

module sbModule 'modules/sbus.bicep' = {
  name: 'sbDeployment'
  params: {
    location: spokeLocation
    name: sbNamespace
    tags: tags
  }
}

module cosmosModule 'modules/cosmosdb.bicep' = {
  name: 'cosmosDeployment'
  params: {
    name: cosmosName
    location: spokeLocation
    dbName: cosmosDbName
    partitionKey: cosmosPartitionKey
    tags: tags
  }
}

// create private DNS zones
resource postGreSQLPrivateDnsZone 'Microsoft.Network/privateDnsZones@2020-06-01' = {
  name: '${spokeLocation}.postgres.database.azure.com'
  location: 'global'
}

// link DNS zones to vnets
resource spokeToHubPrivateDnsZoneLink 'Microsoft.Network/privateDnsZones/virtualNetworkLinks@2020-06-01' = {
  name: '${spokeLocation}-to-${hubLocation}-vnet-link'
  location: 'global'
  parent: postGreSQLPrivateDnsZone
  properties: {
    registrationEnabled: false
    virtualNetwork: {
      id: spokeVnetModule.outputs.vnet.id
    }
  }
}

resource hubToSpokeDnsZoneLink 'Microsoft.Network/privateDnsZones/virtualNetworkLinks@2020-06-01' = {
  name: '${hubLocation}-to-${spokeLocation}-vnet-link'
  location: 'global'
  parent: postGreSQLPrivateDnsZone
  properties: {
    registrationEnabled: false
    virtualNetwork: {
      id: hubVnetModule.outputs.vnet.id
    }
  }
}

// PostGeSQL server & database
module postGreSQLModule './modules/postgesql-flex.bicep' = {
  dependsOn: [
    hubToSpokeDnsZoneLink
    spokeToHubPrivateDnsZoneLink
  ]
  name: 'module-postgresql'
  params: {
    adminLoginName: dbAdminUserName
    adminPassword: dbAdminPassword
    postGreSQLDbName: 'checkindb'
    storageSizeGB: dbSizeGB
    dnsZoneId: postGreSQLPrivateDnsZone.id
    location: hubLocation
    name: 'postgresql-${suffix}'
    subnetId: hubVnetModule.outputs.vnet.subnets[0].id
  }
}

resource containerAppEnvironment 'Microsoft.App/managedEnvironments@2022-01-01-preview' = {
  location: spokeLocation
  name: containerAppEnvName
  properties: {
    daprAIInstrumentationKey: aiModule.outputs.aiKey
    vnetConfiguration: {
      infrastructureSubnetId: spokeVnetModule.outputs.vnet.subnets[0].id
      runtimeSubnetId: spokeVnetModule.outputs.vnet.subnets[1].id
      internal: false
    }
    appLogsConfiguration: {
      destination: 'log-analytics'
      logAnalyticsConfiguration: {
        customerId: wksModule.outputs.workspaceCustomerId
        sharedKey: wksModule.outputs.workspaceSharedKey
      }
    }
  }
  tags: tags
}

resource frontEndApp 'Microsoft.App/containerApps@2022-01-01-preview' = {
  name: frontendAppName
  location: spokeLocation
  tags: tags
  identity: {
    type: 'SystemAssigned'
  }
  properties: {
    configuration: {
      activeRevisionsMode: 'single'
      dapr: {
        appId: frontendAppName
        appPort: 80
        appProtocol: 'http'
        enabled: true
      }
      secrets: secrets
      registries: [
        {
          passwordSecretRef: 'registry-password'
          server: acrLoginServer
          username: acr.name
        }
      ]
      ingress: {
        external: true
        targetPort: 80
        traffic: [
          {
            latestRevision: true
            weight: 100
          }
        ]
        transport: 'http'
      }
    }
    managedEnvironmentId: containerAppEnvironment.id
    template: {
      containers: [
        {
          image: frontEndContainerImage
          name: frontendAppName
          resources: {
            cpu: '0.25'
            memory: '0.5Gi'
          }
          env: [
            {
              name: 'SERVICE_NAME'
              value: frontendAppName
            }
            {
              name: 'SERVICE_PORT'
              value: '80'
            }
            {
              name: 'QUEUE_BINDING_NAME'
              value: 'servicebus'
            }
            {
              name: 'QUEUE_NAME'
              value: sbModule.outputs.queueName
            }
          ]
        }
      ]
      scale: {
        minReplicas: 1
        maxReplicas: 10
      }
    }
  }
}

resource backEndApp 'Microsoft.App/containerApps@2022-01-01-preview' = {
  name: backendAppName
  location: spokeLocation
  tags: tags
  identity: {
    type: 'SystemAssigned'
  }
  properties: {
    configuration: {
      activeRevisionsMode: 'single'
      secrets: secrets
      registries: [
        {
          passwordSecretRef: 'registry-password'
          server: acrLoginServer
          username: acr.name
        }
      ]
      dapr: {
        appId: backendAppName
        appPort: 81
        appProtocol: 'http'
        enabled: true
      }
      ingress: {
        external: false
        targetPort: 81
        traffic: [
          {
            latestRevision: true
            weight: 100
          }
        ]
        transport: 'http'
      }
    }
    managedEnvironmentId: containerAppEnvironment.id
    template: {
      containers: [
        {
          image: backendContainerImage
          name: backendAppName
          resources: {
            cpu: '0.25'
            memory: '0.5Gi'
          }
          env: [
            {
              name: 'SERVICE_NAME'
              value: backendAppName
            }
            {
              name: 'SERVICE_PORT'
              value: '81'
            }
            {
              name: 'QUEUE_BINDING_NAME'
              value: 'servicebus'
            }
            {
              name: 'STORE_BINDING_NAME'
              value: 'cosmosdb'
            }
          ]
        }
      ]
      scale: {
        minReplicas: 1
        maxReplicas: 10
      }
    }
  }
}

resource serviceBusDaprComponent 'Microsoft.App/managedEnvironments/daprComponents@2022-01-01-preview' = {
  name: 'servicebus'
  parent: containerAppEnvironment
  properties: {
    componentType: 'bindings.azure.servicebusqueues'
    version: 'v1'
    ignoreErrors: false
    initTimeout: '5s'
    metadata: [
      {
        name: 'connectionString'
        value: sbModule.outputs.connectionString
      }
      {
        name: 'queueName'
        value: sbModule.outputs.queueName
      }
      {
        name: 'ttlInSeconds'
        value: '60'
      }
    ]
    scopes: [
      frontEndApp.name
      backEndApp.name
    ]
  }
}

resource cosmosDbDaprComponent 'Microsoft.App/managedEnvironments/daprComponents@2022-01-01-preview' = {
  name: 'cosmosdb'
  parent: containerAppEnvironment
  properties: {
    componentType: 'bindings.azure.cosmosdb'
    version: 'v1'
    ignoreErrors: false
    initTimeout: '60s'
    metadata: [
      {
        name: 'url'
        value: cosmosModule.outputs.endpointUri
      }
      {
        name: 'masterKey'
        value: cosmosModule.outputs.masterKey
      }
      {
        name: 'database'
        value: cosmosModule.outputs.dbName
      }
      {
        name: 'collection'
        value: cosmosModule.outputs.collectionName
      }
      {
        name: 'partitionKey'
        value: cosmosPartitionKey
      }
    ]
    scopes: [
      backEndApp.name
    ]
  }
}

output frontendFqdn string = frontEndApp.properties.latestRevisionFqdn
output backendFqdn string = backEndApp.properties.latestRevisionFqdn
